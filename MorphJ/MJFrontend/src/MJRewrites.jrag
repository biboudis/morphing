aspect MJRewrites {

    public void GenericTypeDecl.addConstraintTo(TypeAccess ta, String cons ) {
	// find the type parameter with the name ta.
	// make it a ConstrainedTypeVariable, constrained to be "cons"
	// which could be  "class" or "interface"
	for ( int i=0; i<getNumTypeParameter(); i++) {
	    if ( getTypeParameter(i).getID().equals(ta.getID())) {
		TypeVariable tv = getTypeParameter(i);
		TypeVariable newTv = new ConstrainedTypeVariable(tv.getModifiers(), ta.getID(), tv.getBodyDeclList(), tv.getTypeBoundList(), cons);
		this.setTypeParameter(newTv, i);
		break;
	    }
	}
	return;
    }
    
    public static int Program.generatedTypeVariableCount = 0;
    public static String Program.generateFreshTypeVariableName() {
	return "__MJT_" + generatedTypeVariableCount++;
    }
    public static int Program.generatedNameVariableCount = 0;
    public static String Program.generateFreshNameVariableName() {
	return "__MJN_" + generatedNameVariableCount++;
    }


    rewrite IteratorDecl {
	when ( getNumNestedPattern() == 0 ) to IteratorDecl {
	    TrueNestedPattern tnp = new TrueNestedPattern(null);
	    tnp.bypassErrorChecking = true;
	    addNestedPattern(tnp);
	    return this;
	}
    }

    // Rewrite patterns with missing types to patterns where the type
    // is AllMatchingTypeVariable.
    rewrite OptFieldPattern {
	to FieldPattern {
	    // if there's no modifier declared, then we use AnyModifier
	    Modifiers mods = 
		getModifiers().enhanceWithMaybes(Modifiables.FIELD,
						 getBaseSetNoTransform().type());
	    if ( getTypeAccessOptNoTransform().getChildNoTransform(0) != null ) {
		return new FieldPattern(getBaseSetNoTransform(), mods,
					getTypeAccessOptNoTransform().getChildNoTransform(0), getID(), new Opt(), new List());
	    } else {
		AllMatchingTypeVariable R = new AllMatchingTypeVariable(new Modifiers(), Program.generateFreshTypeVariableName(), new List(), new List().add(typeEveryType().createBoundAccess()));	
		R.addedByPatternRewrite = true;
		enclosingRefIterator().addPatternTypeParameter(R);
		return new FieldPattern(getBaseSetNoTransform(), mods,
					R.createQualifiedAccess(), getID(),
					new Opt(), new List());
	    }
	}
    }
    rewrite OptNestedFieldPattern {
	to NestedFieldPattern {
	    Modifiers mods = 
		getModifiers().enhanceWithMaybes(Modifiables.FIELD,
						 getBaseSetNoTransform().type());
	    if ( getTypeAccessOptNoTransform().getNumChild() != 0 ) {
		return new NestedFieldPattern(getBaseSetNoTransform(), mods,
					      getTypeAccessOptNoTransform().getChildNoTransform(0),
					      getID(),
					      new Opt(), new List(),
					      getIsPositive(),
					      getIsErrorCondition());
	    } else {
		AllMatchingTypeVariable R = new AllMatchingTypeVariable(new Modifiers(), Program.generateFreshTypeVariableName(), new List(), new List().add(typeEveryType().createBoundAccess()));	
		R.addedByPatternRewrite = true;

		enclosingRefIterator().addPatternTypeParameter(R);
		return new NestedFieldPattern(getBaseSetNoTransform(), mods,
					      R.createQualifiedAccess(),
					      getID(), new Opt(), new List(),
					      getIsPositive(),
					      getIsErrorCondition());
	    }
	}
    }
    rewrite OptMethodPattern {
	to MethodPattern {
	    Modifiers mods = 
		getModifiers().enhanceWithMaybes(Modifiables.METHOD, 
						 getBaseSetNoTransform().type());
	    if ( getTypeAccessOptNoTransform().getNumChild() != 0 ) {
		return new MethodPattern(getBaseSetNoTransform(),
					 mods,
					 getTypeAccessOptNoTransform().getChildNoTransform(0),
					 getID(),
					 getParameterListNoTransform(),
					 getExceptionListNoTransform(),
					 new Opt(), new List());
	    } else {
		// generate a type variable with fresh name.
		AllMatchingTypeVariable R = new AllMatchingTypeVariable(new Modifiers(), Program.generateFreshTypeVariableName(), new List(), new List().add(typeEveryType().createBoundAccess()));	
		R.addedByPatternRewrite = true;
		enclosingRefIterator().addPatternTypeParameter(R);
		return new MethodPattern(getBaseSetNoTransform(), mods,
					 R.createQualifiedAccess(),getID(),
					 getParameterListNoTransform(), 
					 getExceptionListNoTransform(), 
					 new Opt(), new List());
	    }
	}
    }
    rewrite OptNestedMethodPattern { 
	to NestedMethodPattern {
	    Modifiers mods = 
		getModifiers().enhanceWithMaybes(Modifiables.METHOD,
						 getBaseSetNoTransform().type());
	    if ( getTypeAccessOptNoTransform().getNumChild() != 0 ) {
		return new NestedMethodPattern(getBaseSetNoTransform(), mods,
					       getTypeAccessOptNoTransform().getChildNoTransform(0),
					       getID(),
					       getParameterListNoTransform(), 
					       getExceptionListNoTransform(), 
					       new Opt(), new List(),
					       getIsPositive(),
					       getIsErrorCondition());
					       
	    } else {
		// generate a type variable with fresh name.
		AllMatchingTypeVariable R = new AllMatchingTypeVariable(new Modifiers(), Program.generateFreshTypeVariableName(), new List(), new List().add(typeEveryType().createBoundAccess()));	
		R.addedByPatternRewrite = true;
		enclosingRefIterator().addPatternTypeParameter(R);
		return new NestedMethodPattern(getBaseSetNoTransform(), mods,
					       R.createQualifiedAccess(),
					       getID(),
					       getParameterListNoTransform(), 
					       getExceptionListNoTransform(), 
					       new Opt(), new List(),
					       getIsPositive(),
					       getIsErrorCondition());
	    }
	}
    }

    // The default is if you do not specify any exceptions, then
    // an exception E* parameter is automatically added to match exceptions.
    // TODO: we need to add a way for users to explicitly match NO exceptions.
    rewrite MethodPattern {
	when ( getNumException() == 0 ) to MethodPattern {
	    VectorTypeVariable E = new VectorTypeVariable(new Modifiers(), Program.generateFreshTypeVariableName(), new List(), new List().add(typeThrowable().createBoundAccess()));
	    E.addedByPatternRewrite = true;
	    enclosingRefIterator().addPatternTypeParameter(E);
	    return new MethodPattern(getBaseSetNoTransform(), getModifiers(),
				     getTypeAccessNoTransform(),
				     getID(),
				     getParameterListNoTransform(),
				     new List().add(E.createQualifiedAccess()),
				     new Opt(), new List());
	}
    }
    rewrite NestedMethodPattern {
	when ( getNumException() == 0 ) to NestedMethodPattern {
	    VectorTypeVariable E = new VectorTypeVariable(new Modifiers(), Program.generateFreshTypeVariableName(), new List(), new List().add(typeThrowable().createBoundAccess()));
	    E.addedByPatternRewrite = true;
	    enclosingRefIterator().addPatternTypeParameter(E);
	    return new NestedMethodPattern(getBaseSetNoTransform(), 
					   getModifiers(),
					   getTypeAccessNoTransform(),
					   getID(),
					   getParameterListNoTransform(),
					   new List().add(E.createQualifiedAccess()),
					   new Opt(), new List(),
					   getIsPositive(),
					   getIsErrorCondition());
	}
    }

    public boolean ASTNode.addedByPatternRewrite = false;
    syn lazy boolean ASTNode.addedByPatternRewrite() = addedByPatternRewrite;
    eq Access.addedByPatternRewrite() = 
	addedByPatternRewrite || type().addedByPatternRewrite();

    // this is to bypass the rewrite for TypeVariable, which adds
    // a bound Object for all type variables.
    rewrite PatternTypeVariable {
	when ( getNumTypeBound() == 0  ) 
	    to PatternTypeVariable {
	    Access boundAccess = null;
	    if ( matchingException()) {
		boundAccess = typeThrowable().createBoundAccess();
	    } else {
		boundAccess = typeNonVoid().createBoundAccess(); 
	    }
	    boundAccess.addedByPatternRewrite = true;
	    addTypeBound(boundAccess);		

	    return this;
	}
    }
    rewrite AllMatchingTypeVariable {
	when ( getNumTypeBound() == 0 )
	    to AllMatchingTypeVariable {
	    Access boundAccess = null;
	    if ( matchingException()) 
		boundAccess = typeThrowable().createBoundAccess();
	    else
		boundAccess = typeEveryType().createBoundAccess();
	    addTypeBound(boundAccess);
	    return this;
	}
    }
}

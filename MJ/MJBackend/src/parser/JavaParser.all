%header {:
 package parser;
 import AST.*;
:};
%embed {:
  class Events extends Parser.Events {
    public void scannerError(Scanner.Exception e) {
      errors.add(new Problem(null, e.getMessage(), e.line, e.column, Problem.Severity.ERROR, Problem.Kind.LEXICAL));
    }
    public void syntaxError(Symbol token) {
      int line = token.getLine(token.getStart());
      int column = token.getColumn(token.getStart());
      int endLine = token.getLine(token.getEnd());
      int endColumn = token.getColumn(token.getEnd());
      String value = token.value != null ? token.value.toString() : Terminals.NAMES[token.getId()];
      errors.add(new Problem(null, "unexpected token \"" + value + "\"", line, column, endLine, endColumn, Problem.Severity.ERROR, Problem.Kind.SYNTACTIC));
    }
    public void unexpectedTokenRemoved(Symbol token) {
    }
    public void missingTokenInserted(Symbol token) {
    }
    public void misspelledTokenReplaced(Symbol token) {
    }
    public void errorPhraseRemoved(Symbol error) {
    }
  }

        {
            report = new Events(); // Use error handler in parser
        }

   public CompilationUnit parse(java.io.InputStream is, String fileName) throws java.io.IOException, beaver.Parser.Exception {
     CompilationUnit cu;
     errors = new ArrayList();
     try {
       scanner.JavaScanner scanner = new scanner.JavaScanner(new scanner.Unicode(is));
       cu = (CompilationUnit)parse(scanner);
     } catch(Parser.Exception e) {
       // build empty compilation unit for failed error recovery
       cu = new CompilationUnit();
     }
     for(java.util.Iterator iter = errors.iterator(); iter.hasNext(); ) {
       Problem p = (Problem)iter.next();
       p.setFileName(fileName);
       cu.addParseError(p);
     }
     return cu;
   }
   protected java.util.Collection errors = new ArrayList();
:};

CompilationUnit goal =
    compilation_unit.cu      {: return cu; :}
  ;
      
Expr literal =
    INTEGER_LITERAL.i        {: return new IntegerLiteral(i); :}
  | LONG_LITERAL.l           {: return new LongLiteral(l); :}
  | FLOATING_POINT_LITERAL.f {: return new FloatingPointLiteral(f); :}
  | DOUBLE_LITERAL.d         {: return new DoubleLiteral(d); :}
  | BOOLEAN_LITERAL.b        {: return new BooleanLiteral(b); :}
  | CHARACTER_LITERAL.c      {: return new CharacterLiteral(c); :}
  | STRING_LITERAL.s         {: return new StringLiteral(s); :}
  | NULL_LITERAL             {: return new NullLiteral("null"); :}
  ;
Access type =
    primitive_type.t         {: return t; :}
  | reference_type.t         {: return t; :}
  ;
Access primitive_type =
    numeric_type.t           {: return t; :}
  | BOOLEAN                  {: return new PrimitiveTypeAccess("boolean"); :}
  ;
Access numeric_type =
    integral_type.t          {: return t; :}
  | floating_point_type.t    {: return t; :}
  ;
Access integral_type =
    BYTE                     {: return new PrimitiveTypeAccess("byte"); :}
  | SHORT                    {: return new PrimitiveTypeAccess("short"); :}
  | INT                      {: return new PrimitiveTypeAccess("int"); :}
  | LONG                     {: return new PrimitiveTypeAccess("long"); :}
  | CHAR                     {: return new PrimitiveTypeAccess("char"); :}
  ;
Access floating_point_type =
    FLOAT                    {: return new PrimitiveTypeAccess("float"); :}
  | DOUBLE                   {: return new PrimitiveTypeAccess("double"); :}
  ;
Access reference_type =
    class_or_interface_type.t {: return t; :}
  | array_type.t              {: return t; :}
  ;
Access class_or_interface_type =
    name.n                    {: return n; :}
  ;
Access class_type =
    class_or_interface_type.n {: return n; :}
  ;
Access interface_type =
    class_or_interface_type.n {: return n; :}
  ;
Access array_type =
    primitive_type.t dims.d   {: return new ArrayTypeAccess(t, d.getNumChild()); :}
  | name.n dims.d             {: return new ArrayTypeAccess(n, d.getNumChild()); :}
  ;
Access name =
    simple_name.s             {: return s; :}
  | qualified_name.q          {: return q; :}
  ;
Access simple_name =
    IDENTIFIER.i              {: return new ParseName(i); :}
  ;
Access qualified_name =
    name.n DOT simple_name.i   {: return n.qualifiesAccess(i); :}
  ;
CompilationUnit compilation_unit =
    package_declaration.p import_declarations.i type_declarations.t {: return new CompilationUnit(p.getID(), i, t); :}
  |                       import_declarations.i type_declarations.t {: return new CompilationUnit("", i, t); :}
  | package_declaration.p                       type_declarations.t {: return new CompilationUnit(p.getID(), new List(), t); :}
  |                                             type_declarations.t {: return new CompilationUnit("", new List(), t); :}
  | package_declaration.p import_declarations.i                     {: return new CompilationUnit(p.getID(), i, new List()); :}
  |                       import_declarations.i                     {: return new CompilationUnit("", i, new List()); :}
  | package_declaration.p                                           {: return new CompilationUnit(p.getID(), new List(), new List()); :}
  |                                                                 {: return new CompilationUnit("", new List(), new List()); :}
  ;
List import_declarations =
    import_declaration.i                       {: return new List().add(i); :}
  | import_declarations.l import_declaration.i {: return l.add(i); :}
  ;
List type_declarations =
    type_declaration.t                         {: return !(t instanceof EmptyType) ? new List().add(t) : new List() ; :}
  | type_declarations.l type_declaration.t     {: return !(t instanceof EmptyType) ? l.add(t) : l; :}
  ;
IdUse package_declaration =
    PACKAGE name_decl.n SEMICOLON              {: return n; :}
  ;
IdUse name_decl =
    simple_name_decl.s        {: return s; :}
  | qualified_name_decl.q     {: return q; :}
  ;
IdUse simple_name_decl =
    IDENTIFIER.i              {: return new IdUse(i); :}
  ;
IdUse qualified_name_decl =
    name_decl.n DOT IDENTIFIER.i {: return new IdUse(n.getID() + "." + i); :}
  ;
ImportDecl import_declaration =
    single_type_import_declaration.s           {: return s; :}
  | type_import_on_demand_declaration.t        {: return t; :}
  ;
ImportDecl single_type_import_declaration =
    IMPORT name.n SEMICOLON                    {: return new SingleTypeImportDecl(n); :}
  ;
ImportDecl type_import_on_demand_declaration =
    IMPORT name.n DOT MULT SEMICOLON           {: return new TypeImportOnDemandDecl(n); :}
  ;
TypeDecl type_declaration =
    class_declaration.c                        {: return c; :}
  | interface_declaration.i                    {: return i; :}
  | SEMICOLON                                  {: return new EmptyType(new Modifiers(), "EmptyType", new Opt(), new List()); :}
  ;
Modifiers modifiers =
    modifier.m                                 {: Modifiers ms = new Modifiers(); ms.addModifier(m); return ms; :}
  | modifiers.l modifier.m                     {: l.addModifier(m); return l; :}
  ;
Modifier modifier =
    PUBLIC                                     {: return new Modifier("public"); :}
  | PROTECTED                                  {: return new Modifier("protected"); :}
  | PRIVATE                                    {: return new Modifier("private"); :}
  | STATIC                                     {: return new Modifier("static"); :}
  | ABSTRACT                                   {: return new Modifier("abstract"); :}
  | FINAL                                      {: return new Modifier("final"); :}
  | NATIVE                                     {: return new Modifier("native"); :}
  | SYNCHRONIZED                               {: return new Modifier("synchronized"); :}
  | TRANSIENT                                  {: return new Modifier("transient"); :}
  | VOLATILE                                   {: return new Modifier("volatile"); :}
  | STRICTFP                                   {: return new Modifier("strictfp"); :}
  ;
ClassDecl class_declaration =
    modifiers.m CLASS IDENTIFIER.id super.s interfaces.i class_body.b 
                                               {: return new ClassDecl(m, id, s, i, b); :}
  |             CLASS IDENTIFIER.id super.s interfaces.i class_body.b 
                                               {: return new ClassDecl(new Modifiers(), id, s, i, b); :}
  | modifiers.m CLASS IDENTIFIER.id         interfaces.i class_body.b 
                                               {: return new ClassDecl(m, id, new Opt(), i, b); :}
  |             CLASS IDENTIFIER.id         interfaces.i class_body.b 
                                               {: return new ClassDecl(new Modifiers(), id, new Opt(), i, b); :}
  | modifiers.m CLASS IDENTIFIER.id super.s              class_body.b 
                                               {: return new ClassDecl(m, id, s, new List(), b); :}
  |             CLASS IDENTIFIER.id super.s              class_body.b 
                                               {: return new ClassDecl(new Modifiers(), id, s, new List(), b); :}
  | modifiers.m CLASS IDENTIFIER.id                      class_body.b 
                                               {: return new ClassDecl(m, id, new Opt(), new List(), b); :}
  |             CLASS IDENTIFIER.id                      class_body.b 
                                               {: return new ClassDecl(new Modifiers(), id, new Opt(), new List(), b); :}
  ;
Opt super =
    EXTENDS class_type.c                       {: return new Opt(c); :}
  ;
List interfaces =
    IMPLEMENTS interface_type_list.i           {: return i; :}
  ;
List interface_type_list =
    interface_type.i                              {: return new List().add(i); :}
  | interface_type_list.l COMMA interface_type.i  {: return l.add(i); :}
  ;
List class_body =
    LBRACE class_body_declarations.c RBRACE    {: return c; :}
  | LBRACE                           RBRACE    {: return new List(); :}
  ;
List class_body_declarations =
    class_body_declaration.c                           {: return new List().add(c); :}
  | class_body_declarations.l class_body_declaration.c {: return l.add(c); :}
  ;
BodyDecl class_body_declaration =
    class_member_declaration.c                 {: return c; :}
  | instance_initializer.i                     {: return i; :}
  | static_initializer.si                      {: return si; :}
  | constructor_declaration.c                  {: return c; :}
  ;
BodyDecl class_member_declaration =
    field_declaration.f                        {: return f; :}
  | method_declaration.m                       {: return m; :}
  | class_declaration.c                        {: return new MemberClassDecl(c); :}
  | interface_declaration.i                    {: return new MemberInterfaceDecl(i); :}
  | SEMICOLON                                  {: return new InstanceInitializer(new Block()); :}
  ;
BodyDecl field_declaration =
    modifiers.m type.t variable_declarators.v SEMICOLON {: return new FieldDecl(m, t, v); :}
  |             type.t variable_declarators.v SEMICOLON {: return new FieldDecl(new Modifiers(), t, v); :}
  ;
List variable_declarators =
    variable_declarator.v                              {: return new List().add(v); :}
  | variable_declarators.l COMMA variable_declarator.v {: return l.add(v); :}
  ;
VariableDecl variable_declarator =
    variable_declarator_id.v                           {: return v; :}
  | variable_declarator_id.v EQ variable_initializer.i {: v.setInit(i); return v; :}
  ;
VariableDecl variable_declarator_id =
    IDENTIFIER.id                              {: return new VariableDecl(id, new List(), new Opt()); :}
  | variable_declarator_id.v LBRACK RBRACK     {: v.addEmptyBracket(new EmptyBracket()); return v; :}
  ;
Expr variable_initializer =
    expression.e                               {: return e; :}
  | array_initializer.a                        {: return a; :}
  ;
MethodDecl method_declaration =
    method_header.m method_body.b              {: m.setBlockOpt(b); return m; :}
  ;
      
MethodDecl method_header =
    modifiers.m type.t method_declarator.d throws.tl 
                                                 {: d.setModifiers(m); d.setTypeAccess(t); d.setExceptionList(tl); return d; :}
  |             type.t method_declarator.d throws.tl 
                                                 {: d.setModifiers(new Modifiers()); d.setTypeAccess(t); d.setExceptionList(tl); return d; :}
  | modifiers.m type.t method_declarator.d 
                                                 {: d.setModifiers(m); d.setTypeAccess(t); d.setExceptionList(new List()); return d; :}
  |             type.t method_declarator.d 
                                      {: d.setModifiers(new Modifiers()); d.setTypeAccess(t); d.setExceptionList(new List()); return d; :}
  | modifiers.m VOID method_declarator.d throws.tl
    {: d.setModifiers(m); d.setTypeAccess(new PrimitiveTypeAccess("void")); d.setExceptionList(tl); return d; :}
  |             VOID method_declarator.d throws.tl
    {: d.setModifiers(new Modifiers()); d.setTypeAccess(new PrimitiveTypeAccess("void")); d.setExceptionList(tl); return d; :}
  | modifiers.m VOID method_declarator.d 
    {: d.setModifiers(m); d.setTypeAccess(new PrimitiveTypeAccess("void")); d.setExceptionList(new List()); return d; :}
  |             VOID method_declarator.d
    {: d.setModifiers(new Modifiers()); d.setTypeAccess(new PrimitiveTypeAccess("void")); d.setExceptionList(new List()); return d; :}
  ;
                                                                
MethodDecl method_declarator =
    IDENTIFIER.id LPAREN formal_parameter_list.l RPAREN {: return new MethodDecl(null, null, id, l, new List(), null, new Opt()); :}
  | IDENTIFIER.id LPAREN                         RPAREN {: return new MethodDecl(null, null, id, new List(), new List(), null, new Opt()); :}
  | method_declarator.m LBRACK RBRACK                   {: m.addEmptyBracket(new EmptyBracket()); return m; :}
  ;
List formal_parameter_list =
    formal_parameter.f                                  {: return new List().add(f); :}
  | formal_parameter_list.l COMMA formal_parameter.f    {: return l.add(f); :}
  ;
ParameterDeclaration formal_parameter =
                type.t variable_declarator_id.v {: return new ParameterDeclaration(new Modifiers(), t, v.getID(), v.getEmptyBracketList()); :}
  | modifiers.m type.t variable_declarator_id.v {: return new ParameterDeclaration(m, t, v.getID(), v.getEmptyBracketList()); :}
  ;
List throws =
    THROWS class_type_list.l                   {: return l; :}
  ;
List class_type_list =
    class_type.c                               {: return new List().add(c); :}
  | class_type_list.l COMMA class_type.c       {: return l.add(c); :}
  ;
Opt method_body =
    block.b                                    {: return new Opt(b); :}
  | SEMICOLON                                  {: return new Opt(); :}
  ;
StaticInitializer static_initializer =
    STATIC block.b                             {: return new StaticInitializer(b); :}
  ;
InstanceInitializer instance_initializer =
    block.b                                    {: return new InstanceInitializer(b); :}
  ;
ConstructorDecl constructor_declaration =
    modifiers.m IDENTIFIER.id LPAREN formal_parameter_list.pl RPAREN throws.tl constructor_body.b
    {: b.setModifiers(m); b.setID(id); b.setParameterList(pl); b.setExceptionList(tl); return b; :}
  |             IDENTIFIER.id LPAREN formal_parameter_list.pl RPAREN throws.tl constructor_body.b
    {: b.setID(id); b.setParameterList(pl); b.setExceptionList(tl); return b; :}
  | modifiers.m IDENTIFIER.id LPAREN                          RPAREN throws.tl constructor_body.b
    {: b.setModifiers(m); b.setID(id); b.setExceptionList(tl); return b; :}
  |             IDENTIFIER.id LPAREN                          RPAREN throws.tl constructor_body.b
    {: b.setID(id); b.setExceptionList(tl); return b; :}
  | modifiers.m IDENTIFIER.id LPAREN formal_parameter_list.pl RPAREN           constructor_body.b
    {: b.setModifiers(m); b.setID(id); b.setParameterList(pl); return b; :}
  |             IDENTIFIER.id LPAREN formal_parameter_list.pl RPAREN           constructor_body.b
    {: b.setID(id); b.setParameterList(pl); return b; :}
  | modifiers.m IDENTIFIER.id LPAREN                          RPAREN           constructor_body.b
    {: b.setModifiers(m); b.setID(id); return b; :}
  |             IDENTIFIER.id LPAREN                          RPAREN           constructor_body.b
    {: b.setID(id); return b; :}
 ;
ConstructorDecl constructor_body =
		LBRACE explicit_constructor_invocation.c block_statements.l RBRACE
    {: return new ConstructorDecl(new Modifiers(), null, new List(), new List(), new Opt(c), new Block(l)); :}
	|	LBRACE explicit_constructor_invocation.c RBRACE
    {: return new ConstructorDecl(new Modifiers(), null, new List(), new List(), new Opt(c), new Block(new List())); :}
	|	LBRACE block_statements.l RBRACE
    {: return new ConstructorDecl(new Modifiers(), null, new List(), new List(), new Opt(), new Block(l)); :}
	|	LBRACE RBRACE
    {: return new ConstructorDecl(new Modifiers(), null, new List(), new List(), new Opt(), new Block(new List())); :}
	;
ExprStmt explicit_constructor_invocation =
    THIS LPAREN argument_list_opt.l RPAREN SEMICOLON 
    {: ConstructorAccess c = new ConstructorAccess("this", l);
       c.setStart(THIS.getStart());
       c.setEnd(RPAREN.getEnd());
       return new ExprStmt(c); :}
  | SUPER LPAREN argument_list_opt.l RPAREN SEMICOLON 
    {: SuperConstructorAccess c = new SuperConstructorAccess("super", l);
       c.setStart(SUPER.getStart());
       c.setEnd(RPAREN.getEnd());
       return new ExprStmt(c); :}
  | primary.p DOT SUPER LPAREN argument_list_opt.l RPAREN SEMICOLON
    {: SuperConstructorAccess c = new SuperConstructorAccess("super", l);
       c.setStart(SUPER.getStart());
       c.setEnd(RPAREN.getEnd());
       return new ExprStmt(p.qualifiesAccess(c)); :}
  | name.n DOT SUPER LPAREN argument_list_opt.l RPAREN SEMICOLON
    {: SuperConstructorAccess c = new SuperConstructorAccess("super", l);
       c.setStart(SUPER.getStart());
       c.setEnd(RPAREN.getEnd());
       return new ExprStmt(n.qualifiesAccess(c)); :}
  ;
InterfaceDecl interface_declaration =
    modifiers.m INTERFACE IDENTIFIER.id extends_interfaces.i interface_body.b {: return new InterfaceDecl(m, id, i, b); :}
  |             INTERFACE IDENTIFIER.id extends_interfaces.i interface_body.b {: return new InterfaceDecl(new Modifiers(), id, i, b); :}
  | modifiers.m INTERFACE IDENTIFIER.id                      interface_body.b {: return new InterfaceDecl(m, id, new List(), b); :}
  |             INTERFACE IDENTIFIER.id                      interface_body.b {: return new InterfaceDecl(new Modifiers(), id, new List(), b); :}
  ;
List extends_interfaces =
    EXTENDS interface_type.i                    {: return new List().add(i); :}
  | extends_interfaces.l COMMA interface_type.i {: return l.add(i); :}
  ;
    
List interface_body =
    LBRACE interface_member_declarations.i RBRACE  {: return i; :}
  | LBRACE                                 RBRACE  {: return new List(); :}
  ;   
List interface_member_declarations =
    interface_member_declaration.i                                  {: return new List().add(i); :}
  | interface_member_declarations.l interface_member_declaration.i  {: return l.add(i); :}
  ;
BodyDecl interface_member_declaration =
    constant_declaration.c                     {: return c; :}
  | abstract_method_declaration.a              {: return a; :}
  | class_declaration.c                        {: return new MemberClassDecl(c); :}
  | interface_declaration.i                    {: return new MemberInterfaceDecl(i); :}
  | SEMICOLON                                  {: return new StaticInitializer(new Block()); :}
  ;
BodyDecl constant_declaration =
    field_declaration.f                        {: return f; :}
  ;
BodyDecl abstract_method_declaration =
    method_header.m SEMICOLON                  {: return m; :}
  ;
ArrayInit array_initializer =
    LBRACE variable_initializers.v COMMA RBRACE   {: return new ArrayInit(v); :}
  | LBRACE variable_initializers.v RBRACE         {: return new ArrayInit(v); :}
  | LBRACE COMMA RBRACE                           {: return new ArrayInit(new List()); :}
  | LBRACE RBRACE                                 {: return new ArrayInit(new List()); :}
  ;
List variable_initializers =
    variable_initializer.v                                {: return new List().add(v); :}
  | variable_initializers.l COMMA variable_initializer.v  {: return l.add(v); :}
  ;
Block block =
    LBRACE block_statements.l RBRACE {: return new Block(l); :}
  | LBRACE                    RBRACE {: return new Block(new List()); :}
  ;
List block_statements =
    block_statement.b                     {: return new List().add(b); :}
  | block_statements.l block_statement.b  {: return l.add(b); :}
  ;
Stmt block_statement =
    local_variable_declaration_statement.l  {: return l; :}
  | class_declaration.c                     {: return new LocalClassDeclStmt(c); :}
  | statement.s                             {: return s; :}
  ;
VarDeclStmt local_variable_declaration_statement =
    local_variable_declaration.l SEMICOLON  {: return l; :}
  ;
VarDeclStmt local_variable_declaration =
                 type.t variable_declarators.l {: return new VarDeclStmt(new Modifiers(), t, l); :}
  |  modifiers.m type.t variable_declarators.l {: return new VarDeclStmt(m, t, l); :}
  ;
Stmt statement =
    statement_without_trailing_substatement.s  {: return s; :}
  | labeled_statement.l                        {: return l; :}
  | if_then_statement.i                        {: return i; :}
  | if_then_else_statement.i                   {: return i; :}
  | while_statement.w                          {: return w; :}
  | for_statement.f                            {: return f; :}
  ;
Stmt statement_without_trailing_substatement =
    block.b                                    {: return b; :}
  | empty_statement.e                          {: return e; :}
  | expression_statement.e                     {: return e; :}
  | switch_statement.s                         {: return s; :}
  | do_statement.d                             {: return d; :}
  | break_statement.b                          {: return b; :}
  | continue_statement.c                       {: return c; :}
  | return_statement.r                         {: return r; :}
  | synchronized_statement.s                   {: return s; :}
  | throw_statement.t                          {: return t; :}
  | try_statement.t                            {: return t; :}
  | assert_statement.a                         {: return a; :}
  ;
Stmt statement_no_short_if =
    statement_without_trailing_substatement.s  {: return s; :}
  | labeled_statement_no_short_if.l            {: return l; :}
  | if_then_else_statement_no_short_if.i       {: return i; :}
  | while_statement_no_short_if.w              {: return w; :}
  | for_statement_no_short_if.f                {: return f; :}
  ;
IfStmt if_then_statement =
    IF LPAREN expression.e RPAREN statement.s  {: return new IfStmt(e, s, new Opt()); :}
  ;
IfStmt if_then_else_statement =
    IF LPAREN expression.e RPAREN statement_no_short_if.t ELSE statement.els {: return new IfStmt(e, t, new Opt(els)); :}
  ;
IfStmt if_then_else_statement_no_short_if =
    IF LPAREN expression.e RPAREN statement_no_short_if.t ELSE statement_no_short_if.els {: return new IfStmt(e, t, new Opt(els)); :}
  ;
EmptyStmt empty_statement =
    SEMICOLON                                  {: return new EmptyStmt(); :}
  ;
LabeledStmt labeled_statement =
    IDENTIFIER.id COLON statement.s            {: return new LabeledStmt(id, s); :}
  ;
LabeledStmt labeled_statement_no_short_if =
    IDENTIFIER.id COLON statement_no_short_if.s {: return new LabeledStmt(id, s); :}
  ;
ExprStmt expression_statement =
    statement_expression.e SEMICOLON           {: return e; :}
  ;
ExprStmt statement_expression =
    assignment.a                               {: return new ExprStmt(a); :}
  | preincrement_expression.e                  {: return new ExprStmt(e); :}
  | predecrement_expression.e                  {: return new ExprStmt(e); :}
  | postincrement_expression.e                 {: return new ExprStmt(e); :}
  | postdecrement_expression.e                 {: return new ExprStmt(e); :}
  | method_invocation.i                        {: return new ExprStmt(i); :}
  | class_instance_creation_expression.e       {: return new ExprStmt(e); :}
  ;
SwitchStmt switch_statement =
    SWITCH LPAREN expression.e RPAREN switch_block.l {: return new SwitchStmt(e, l); :}
  ;
Block switch_block =
    LBRACE switch_block_statement_groups.l switch_labels.s RBRACE 
                        {: for(int ii = 0; ii < s.getNumChild(); ii++) l.add(s.getChildNoTransform(ii)); return new Block(l); :}
  | LBRACE switch_block_statement_groups.l RBRACE   {: return new Block(l); :}
  | LBRACE switch_labels.l RBRACE              {: return new Block(l); :}
  | LBRACE RBRACE                              {: return new Block(new List()); :}
  ;
List switch_block_statement_groups =
    switch_block_statement_group.g             {: return g; :}
  | switch_block_statement_groups.l switch_block_statement_group.g 
      {: for(int ii = 0; ii < g.getNumChild(); ii++)
           l.add(g.getChildNoTransform(ii));
         return l; :}
  ;
List switch_block_statement_group =
    switch_labels.l block_statements.bl 
    {:  for(int ii = 0; ii < bl.getNumChild(); ii++)
          l.add(bl.getChildNoTransform(ii));
          return l; :}
  ;
List switch_labels =
    switch_label.s                             {: return new List().add(s); :}
  | switch_labels.l switch_label.s             {: return l.add(s); :}
  ;
Case switch_label =
    CASE constant_expression.e COLON           {: return new ConstCase(e); :}
  | DEFAULT COLON                              {: return new DefaultCase(); :}
  ;
WhileStmt while_statement =
    WHILE LPAREN expression.e RPAREN statement.s {: return new WhileStmt(e, s); :}
  ;
WhileStmt while_statement_no_short_if =
    WHILE LPAREN expression.e RPAREN statement_no_short_if.s {: return new WhileStmt(e, s); :}
  ;
DoStmt do_statement =
    DO statement.s WHILE LPAREN expression.e RPAREN SEMICOLON {: return new DoStmt(s, e); :}
  ;
ForStmt for_statement =
    FOR LPAREN for_init.i SEMICOLON expression.e SEMICOLON for_update.u RPAREN statement.s {: return new ForStmt(i, new Opt(e), u, s); :}
  | FOR LPAREN            SEMICOLON expression.e SEMICOLON for_update.u RPAREN statement.s {: return new ForStmt(new List(), new Opt(e), u, s); :}
  | FOR LPAREN for_init.i SEMICOLON              SEMICOLON for_update.u RPAREN statement.s {: return new ForStmt(i, new Opt(), u, s); :}
  | FOR LPAREN            SEMICOLON              SEMICOLON for_update.u RPAREN statement.s {: return new ForStmt(new List(), new Opt(), u, s); :}
  | FOR LPAREN for_init.i SEMICOLON expression.e SEMICOLON              RPAREN statement.s {: return new ForStmt(i, new Opt(e), new List(), s); :}
  | FOR LPAREN            SEMICOLON expression.e SEMICOLON              RPAREN statement.s {: return new ForStmt(new List(), new Opt(e), new List(), s); :}
  | FOR LPAREN for_init.i SEMICOLON              SEMICOLON              RPAREN statement.s {: return new ForStmt(i, new Opt(), new List(), s); :}
  | FOR LPAREN            SEMICOLON              SEMICOLON              RPAREN statement.s {: return new ForStmt(new List(), new Opt(), new List(), s); :}
  ;
ForStmt for_statement_no_short_if =
    FOR LPAREN for_init.i SEMICOLON expression.e SEMICOLON for_update.u RPAREN statement_no_short_if.s {: return new ForStmt(i, new Opt(e), u, s); :}
  | FOR LPAREN            SEMICOLON expression.e SEMICOLON for_update.u RPAREN statement_no_short_if.s {: return new ForStmt(new List(), new Opt(e), u, s); :}
  | FOR LPAREN for_init.i SEMICOLON              SEMICOLON for_update.u RPAREN statement_no_short_if.s {: return new ForStmt(i, new Opt(), u, s); :}
  | FOR LPAREN            SEMICOLON              SEMICOLON for_update.u RPAREN statement_no_short_if.s {: return new ForStmt(new List(), new Opt(), u, s); :}
  | FOR LPAREN for_init.i SEMICOLON expression.e SEMICOLON              RPAREN statement_no_short_if.s {: return new ForStmt(i, new Opt(e), new List(), s); :}
  | FOR LPAREN            SEMICOLON expression.e SEMICOLON              RPAREN statement_no_short_if.s {: return new ForStmt(new List(), new Opt(e), new List(), s); :}
  | FOR LPAREN for_init.i SEMICOLON              SEMICOLON              RPAREN statement_no_short_if.s {: return new ForStmt(i, new Opt(), new List(), s); :}
  | FOR LPAREN            SEMICOLON              SEMICOLON              RPAREN statement_no_short_if.s {: return new ForStmt(new List(), new Opt(), new List(), s); :}
  ;
List for_init =
    statement_expression_list.l        {: return l; :}
  | local_variable_declaration.d       {: return new List().add(d); :};
List for_update =
    statement_expression_list.l        {: return l; :}
  ;
List statement_expression_list =
    statement_expression.e                                   {: return new List().add(e); :}
  | statement_expression_list.l COMMA statement_expression.e {: return l.add(e); :}
  ;
BreakStmt break_statement =
    BREAK IDENTIFIER.id SEMICOLON              {: return new BreakStmt(id); :}
  | BREAK               SEMICOLON              {: return new BreakStmt(""); :}
  ; 
ContinueStmt continue_statement =
    CONTINUE IDENTIFIER.id SEMICOLON           {: return new ContinueStmt(id); :}
  | CONTINUE               SEMICOLON           {: return new ContinueStmt(""); :}
  ;
ReturnStmt return_statement =
    RETURN expression.e SEMICOLON              {: return new ReturnStmt(new Opt(e)); :}
  | RETURN              SEMICOLON              {: return new ReturnStmt(new Opt()); :}
  ;
ThrowStmt throw_statement =
    THROW expression.e SEMICOLON               {: return new ThrowStmt(e); :}
  ;
SynchronizedStmt synchronized_statement =
    SYNCHRONIZED LPAREN expression.e RPAREN block.b {: return new SynchronizedStmt(e, b); :}
  ;
TryStmt try_statement =
    TRY block.b catches.c                      {: return new TryStmt(b, c, new Opt()); :}
  | TRY block.b catches.c finally.f            {: return new TryStmt(b, c, new Opt(f)); :}
  | TRY block.b           finally.f            {: return new TryStmt(b, new List(), new Opt(f)); :}
  ;
List catches =
    catch_clause.c                             {: return new List().add(c); :}
  | catches.l catch_clause.c                   {: return l.add(c); :}
  ;
CatchClause catch_clause =
    CATCH LPAREN formal_parameter.p RPAREN block.b {: return new CatchClause(p, b); :}
  ;
Block finally =
    FINALLY block.b {: return b; :}
  ;
AssertStmt assert_statement =
    ASSERT expression.e                    SEMICOLON {: return new AssertStmt(e, new Opt()); :}
  | ASSERT expression.e COLON expression.s SEMICOLON {: return new AssertStmt(e, new Opt(s)); :}
  ;
Expr primary =
    primary_no_new_array.p                     {: return p; :}
  | array_creation_init.a                      {: return a; :}
  | array_creation_uninit.a                    {: return a; :}
  ;

ClassAccess class_literal =
  CLASS {: return new ClassAccess(); :}
  ;

Expr primary_no_new_array =
    literal.l                                  {: return l; :}
  | primitive_type.n DOT class_literal.c       {: return n.qualifiesAccess(c); :}
  | primitive_type.n dims.d DOT class_literal.c {: ArrayTypeAccess a = new ArrayTypeAccess(n, d.getNumChild());
                                                   a.setStart(n.getStart());
                                                   a.setEnd(d.getEnd());
                                                   return a.qualifiesAccess(c); :}
  | name.n DOT class_literal.c                 {: return n.qualifiesAccess(c); :}
  | name.n dims.d DOT class_literal.c          {: ArrayTypeAccess a = new ArrayTypeAccess(n, d.getNumChild());
                                                  a.setStart(n.getStart());
                                                  a.setEnd(d.getEnd());
                                                  return a.qualifiesAccess(c); :}
  | VOID DOT class_literal.c                   {: PrimitiveTypeAccess v = new PrimitiveTypeAccess("void");
                                                  v.setStart(VOID.getStart());
                                                  v.setEnd(VOID.getEnd());
                                                  return v.qualifiesAccess(c); :}
  | THIS                                       {: return new ThisAccess("this"); :}
  | name.n DOT THIS                            {: ThisAccess t = new ThisAccess("this");
                                                  t.setStart(THIS.getStart()); t.setEnd(THIS.getEnd());
                                                  return n.qualifiesAccess(t); :}
  | LPAREN expression.e RPAREN                 {: return new ParExpr(e); :}
  | LPAREN name.n RPAREN                       {: return new ParExpr(n); :}
  | class_instance_creation_expression.c       {: return c; :}
  | field_access.f                             {: return f; :}
  | method_invocation.m                        {: return m; :}
  | array_access.a                             {: return a; :}
  ;
Expr class_instance_creation_expression =
    NEW class_or_interface_type.t LPAREN argument_list_opt.l RPAREN 
    {: return new ClassInstanceExpr(t, l, new Opt()); :}
  | primary.n DOT NEW simple_name.id LPAREN argument_list_opt.l RPAREN
    {: ClassInstanceExpr e = new ClassInstanceExpr(id, l, new Opt());
       e.setStart(NEW.getStart());
       e.setEnd(RPAREN.getEnd());
       return n.qualifiesAccess(e); :}
  | name.n DOT NEW simple_name.id LPAREN argument_list_opt.l RPAREN
    {: ClassInstanceExpr e = new ClassInstanceExpr(id, l, new Opt());
       e.setStart(NEW.getStart());
       e.setEnd(RPAREN.getEnd());
       return n.qualifiesAccess(e); :}
  | NEW class_or_interface_type.t LPAREN argument_list_opt.l RPAREN subclass_body.b 
    {: return new ClassInstanceExpr(t, l, b); :}
  | primary.n DOT NEW simple_name.id LPAREN argument_list_opt.l RPAREN subclass_body.b
    {: ClassInstanceExpr e = new ClassInstanceExpr(id, l, b);
       e.setStart(NEW.getStart());
       e.setEnd(b.getEnd());
       return n.qualifiesAccess(e); :}
  | name.n DOT NEW simple_name.id LPAREN argument_list_opt.l RPAREN subclass_body.b
    {: ClassInstanceExpr e = new ClassInstanceExpr(id, l, b);
       e.setStart(NEW.getStart());
       e.setEnd(b.getEnd());
       return n.qualifiesAccess(e); :}
  ;
Opt subclass_body =
    class_body.b                               {: return new Opt(new AnonymousDecl(new Modifiers(), "Anonymous", b)); :}
  ;
List argument_list_opt =
                                               {: return new List(); :}
  | argument_list.a                            {: return a; :}
  ;
List argument_list =
    expression.e                               {: return new List().add(e); :}
  | argument_list.l COMMA expression.e         {: return l.add(e); :}
  ;

Expr array_creation_uninit =
    NEW primitive_type.t dim_exprs.d           {: return new ArrayCreationExpr(t, d, new Opt()); :}
  | NEW primitive_type.t dim_exprs.d dims.e 
         {: for(int i = 0; i < e.getNumChild(); i++) d.add(e.getChildNoTransform(i)); return new ArrayCreationExpr(t, d, new Opt()); :}
  | NEW class_or_interface_type.t dim_exprs.d  {: return new ArrayCreationExpr(t, d, new Opt()); :}
  | NEW class_or_interface_type.t dim_exprs.d dims.e 
         {: for(int i = 0; i < e.getNumChild(); i++) d.add(e.getChildNoTransform(i)); return new ArrayCreationExpr(t, d, new Opt()); :}
  ;
Expr array_creation_init =
    NEW primitive_type.t dims.d array_initializer.i           {: return new ArrayCreationExpr(t, d, new Opt(i)); :}
  | NEW class_or_interface_type.t dims.d array_initializer.i  {: return new ArrayCreationExpr(t, d, new Opt(i)); :}
  ;
List dim_exprs =
    dim_expr.e                                 {: return new List().add(e); :}
  | dim_exprs.l dim_expr.e                     {: return l.add(e); :}
  ;
Dims dim_expr =
    LBRACK expression.e RBRACK                 {: return new Dims(new Opt(e)); :}
  ;
List dims =
    LBRACK RBRACK                              {: return new List().add(new Dims(new Opt())); :}
  | dims.l LBRACK RBRACK                       {: return l.add(new Dims(new Opt())); :}
  ;

Access field_access =
    primary.p DOT simple_name.id                {: return p.qualifiesAccess(id); :}
  | SUPER DOT simple_name.id 
    {: SuperAccess s = new SuperAccess("super");
       s.setStart(SUPER.getStart());
       s.setEnd(SUPER.getEnd());
       return s.qualifiesAccess(id); :}
  | name.n DOT SUPER DOT simple_name.id
    {: SuperAccess s = new SuperAccess("super");
       s.setStart(SUPER.getStart());
       s.setEnd(SUPER.getEnd());
       return n.qualifiesAccess(s).qualifiesAccess(id); :}
  ;
Access method_invocation =
      name.n LPAREN argument_list_opt.l RPAREN {: 
                                                             if(n instanceof AbstractDot) {    
                                                               AbstractDot d = (AbstractDot)n; 
                                                               ParseName pn = (ParseName)d.extractLast();
                                                               MethodAccess m = new MethodAccess(pn.getID(), l);
                                                               m.setStart(n.getStart()); // add location information
                                                               m.setEnd(RPAREN.getEnd()); // add location information
                                                               d.replaceLast(m);
                                                               return d;
                                                             }
                                                             else {
                                                               ParseName pn = (ParseName)n;
                                                               return new MethodAccess(pn.getID(), l);
                                                             }
                                                               :}
   | primary.p DOT IDENTIFIER LPAREN argument_list_opt.l RPAREN 
     {: String id = (String)IDENTIFIER.value; // extract token value
        MethodAccess m = new MethodAccess(id, l);
        m.setStart(IDENTIFIER.getStart()); // add location information
        m.setEnd(RPAREN.getEnd()); // add location information
        return p.qualifiesAccess(m); :}
   | SUPER DOT IDENTIFIER LPAREN argument_list_opt.l RPAREN
     {: String id = (String)IDENTIFIER.value; // extract token value
        SuperAccess s = new SuperAccess("super");
        s.setStart(SUPER.getStart());
        s.setEnd(SUPER.getEnd());
        MethodAccess m = new MethodAccess(id, l);
        m.setStart(IDENTIFIER.getStart());
        m.setEnd(RPAREN.getEnd());
        return s.qualifiesAccess(m); :}
   | name.n DOT SUPER DOT IDENTIFIER LPAREN argument_list_opt.l RPAREN
     {: String id = (String)IDENTIFIER.value; // extract token value
        SuperAccess s = new SuperAccess("super");
        s.setStart(SUPER.getStart());
        s.setEnd(SUPER.getEnd());
        MethodAccess m = new MethodAccess(id, l);
        m.setStart(IDENTIFIER.getStart());
        m.setEnd(RPAREN.getEnd());
        return n.qualifiesAccess(s).qualifiesAccess(m); :}
   ;
Access array_access =
    name.n LBRACK expression.e RBRACK                 {: ArrayAccess a = new ArrayAccess(e);
                                                         a.setStart(LBRACK.getStart());
                                                         a.setEnd(RBRACK.getEnd());
                                                         return n.qualifiesAccess(a); :}
  | primary_no_new_array.p LBRACK expression.e RBRACK {: ArrayAccess a = new ArrayAccess(e);
                                                         a.setStart(LBRACK.getStart());
                                                         a.setEnd(RBRACK.getEnd());
                                                         return p.qualifiesAccess(a); :}
  | array_creation_init.i LBRACK expression.e RBRACK  {: ArrayAccess a = new ArrayAccess(e);
                                                         a.setStart(LBRACK.getStart());
                                                         a.setEnd(RBRACK.getEnd());
                                                         return i.qualifiesAccess(a); :}
  ;
Expr postfix_expression =
    primary.p                                  {: return p; :}
  | name.n                                     {: return n; :}
  | postincrement_expression.e                 {: return e; :}
  | postdecrement_expression.e                 {: return e; :}
  ;
Expr postincrement_expression =
    postfix_expression.e PLUSPLUS              {: return new PostIncExpr(e); :}
  ;
Expr postdecrement_expression =
    postfix_expression.e MINUSMINUS            {: return new PostDecExpr(e); :}
  ;
Expr unary_expression =
    preincrement_expression.e                  {: return e; :}
  | predecrement_expression.e                  {: return e; :}
  | PLUS unary_expression.e                    {: return new PlusExpr(e); :}
  | MINUS unary_expression.e                   {: return new MinusExpr(e); :}
  | unary_expression_not_plus_minus.e          {: return e; :}
  ;
Expr preincrement_expression =
    PLUSPLUS unary_expression.e                {: return new PreIncExpr(e); :}
  ;
Expr predecrement_expression =
    MINUSMINUS unary_expression.e              {: return new PreDecExpr(e); :}
  ;
Expr unary_expression_not_plus_minus =
    postfix_expression.e                       {: return e; :}
  | COMP unary_expression.e                    {: return new BitNotExpr(e); :}
  | NOT unary_expression.e                     {: return new LogNotExpr(e); :}
  | cast_expression.e                          {: return e; :}
  ;
Expr cast_expression =
    LPAREN primitive_type.t dims.d RPAREN unary_expression.e      {: ArrayTypeAccess a = new ArrayTypeAccess(t, d.getNumChild());
                                                                     a.setStart(t.getStart()); a.setEnd(d.getEnd());
                                                                     return new CastExpr(a, e); :}
  | LPAREN primitive_type.t        RPAREN unary_expression.e      {: return new CastExpr(t, e); :}
  | LPAREN name.n dims.d RPAREN unary_expression_not_plus_minus.e {: ArrayTypeAccess a = new ArrayTypeAccess(n, d.getNumChild());
                                                                     a.setStart(n.getStart()); a.setEnd(d.getEnd());
                                                                     return new CastExpr(a, e); :}
  | LPAREN name.n        RPAREN unary_expression_not_plus_minus.e {: return new CastExpr(n, e); :}
  ;
Expr multiplicative_expression =
    unary_expression.e                                     {: return e; :}
  | multiplicative_expression.e1 MULT unary_expression.e2  {: return new MulExpr(e1, e2); :}
  | multiplicative_expression.e1 DIV unary_expression.e2   {: return new DivExpr(e1, e2); :}
  | multiplicative_expression.e1 MOD unary_expression.e2   {: return new ModExpr(e1, e2); :}
  ;
Expr additive_expression =
    multiplicative_expression.e                                {: return e; :}
  | additive_expression.e1 PLUS multiplicative_expression.e2   {: return new AddExpr(e1, e2); :}
  | additive_expression.e1 MINUS multiplicative_expression.e2  {: return new SubExpr(e1, e2); :}
  ;
Expr shift_expression =
    additive_expression.e                               {: return e; :}
  | shift_expression.e1 LSHIFT additive_expression.e2   {: return new LShiftExpr(e1, e2); :}
  | shift_expression.e1 RSHIFT additive_expression.e2   {: return new RShiftExpr(e1, e2); :}
  | shift_expression.e1 URSHIFT additive_expression.e2  {: return new URShiftExpr(e1, e2); :}
  ;
Expr relational_expression =
    shift_expression.e                                  {: return e; :}
  | relational_expression.e1 LT shift_expression.e2     {: return new LTExpr(e1, e2); :}
  | relational_expression.e1 GT shift_expression.e2     {: return new GTExpr(e1, e2); :}
  | relational_expression.e1 LTEQ shift_expression.e2   {: return new LEExpr(e1, e2); :}
  | relational_expression.e1 GTEQ shift_expression.e2   {: return new GEExpr(e1, e2); :}
  | relational_expression.e INSTANCEOF reference_type.t {: return new InstanceOfExpr(e, t); :}
  ;
Expr equality_expression =
    relational_expression.e                               {: return e; :}
  | equality_expression.e1 EQEQ relational_expression.e2  {: return new EQExpr(e1, e2); :}
  | equality_expression.e1 NOTEQ relational_expression.e2 {: return new NEExpr(e1, e2); :}
  ;
Expr and_expression =
    equality_expression.e                               {: return e; :}
  | and_expression.e1 AND equality_expression.e2        {: return new AndBitwiseExpr(e1, e2); :}
  ;
Expr exclusive_or_expression =
    and_expression.e                                    {: return e; :}
  | exclusive_or_expression.e1 XOR and_expression.e2    {: return new XorBitwiseExpr(e1, e2); :}
  ;
Expr inclusive_or_expression =
    exclusive_or_expression.e                                {: return e; :}
  | inclusive_or_expression.e1 OR exclusive_or_expression.e2 {: return new OrBitwiseExpr(e1, e2); :}
  ;
Expr conditional_and_expression =
    inclusive_or_expression.e                                       {: return e; :}
  | conditional_and_expression.e1 ANDAND inclusive_or_expression.e2 {: return new AndLogicalExpr(e1, e2); :}
  ;
Expr conditional_or_expression =
    conditional_and_expression.e                                    {: return e; :}
  | conditional_or_expression.e1 OROR conditional_and_expression.e2 {: return new OrLogicalExpr(e1, e2); :}
  ;
Expr conditional_expression =
    conditional_or_expression.c                                                        {: return c; :}
  | conditional_or_expression.c QUESTION expression.e1 COLON conditional_expression.e2 {: return new ConditionalExpr(c, e1, e2); :}
  ;
Expr assignment_expression =
    conditional_expression.c {: return c; :}
  | assignment.a {: return a; :}
  ;
Expr assignment =
    postfix_expression.dest assignment_operator.e assignment_expression.source {: e.setDest(dest); e.setSource(source);
                                                                                  e.setStart(dest.getStart()); e.setEnd(source.getEnd());
                                                                                  return e; :}
  ;
AssignExpr assignment_operator =
    EQ                                         {: return new AssignSimpleExpr(null, null); :}
  | MULTEQ                                     {: return new AssignMulExpr(null, null); :}
  | DIVEQ                                      {: return new AssignDivExpr(null, null); :}
  | MODEQ                                      {: return new AssignModExpr(null, null); :}
  | PLUSEQ                                     {: return new AssignPlusExpr(null, null); :}
  | MINUSEQ                                    {: return new AssignMinusExpr(null, null); :}
  | LSHIFTEQ                                   {: return new AssignLShiftExpr(null, null); :}
  | RSHIFTEQ                                   {: return new AssignRShiftExpr(null, null); :}
  | URSHIFTEQ                                  {: return new AssignURShiftExpr(null, null); :}
  | ANDEQ                                      {: return new AssignAndExpr(null, null); :}
  | XOREQ                                      {: return new AssignXorExpr(null, null); :}
  | OREQ                                       {: return new AssignOrExpr(null, null); :}
  ;
Expr expression =
    assignment_expression.a                    {: return a; :}
  ;
Expr constant_expression =
    expression.e                               {: return e; :}
  ;
BodyDecl class_body_declaration =
    error SEMICOLON {: return new InstanceInitializer(new Block()); :}
  | error LBRACE {: return new InstanceInitializer(new Block()); :}
  ;

Block block =
    error RBRACE {: return new Block(new List()); :}
  ;

Stmt statement =
    error SEMICOLON {: return new EmptyStmt(); :}
  ;
TypeDecl type_declaration =
  annotation_type_declaration.i {:  return i;  :}
  ;
  
BodyDecl interface_member_declaration =
  annotation_type_declaration.i {:  return new MemberInterfaceDecl(i);  :}
  ;

BodyDecl class_member_declaration =
  annotation_type_declaration.i {:  return new MemberInterfaceDecl(i);  :}
  ;

// a comment
AnnotationDecl annotation_type_declaration =
  modifiers.m AT INTERFACE IDENTIFIER.id annotation_type_body.b 
  {:  return new AnnotationDecl(m, id, b); :}
  | AT INTERFACE IDENTIFIER.id annotation_type_body.b 
  {:  return new AnnotationDecl(new Modifiers(new List()), id, b); :}
  ;

List annotation_type_body =
    LBRACE                                        RBRACE {: return new List(); :}
  | LBRACE annotation_type_element_declarations.i RBRACE {: return i; :}
  ;

List annotation_type_element_declarations =
     annotation_type_element_declaration.i {:  return new List().add(i); :}
  |  annotation_type_element_declarations.l annotation_type_element_declaration.i {:  return l.add(i);  :};

BodyDecl annotation_type_element_declaration =
    modifiers.m type.t IDENTIFIER.id LPAREN RPAREN default_value_opt SEMICOLON
         {: return new AnnotationMethodDecl(m, t, id, new List(), new List(), new List(), new Opt(), default_value_opt); :}
  |             type.t IDENTIFIER.id LPAREN RPAREN default_value_opt SEMICOLON
         {: return new AnnotationMethodDecl(new Modifiers(new List()), t, id, new List(), new List(), new List(), new Opt(), default_value_opt); :}
  | constant_declaration
  | class_declaration {: return new MemberClassDecl(class_declaration); :}
  | interface_declaration {: return new MemberInterfaceDecl(interface_declaration); :}
  | enum_declaration {: return new MemberClassDecl(enum_declaration); :}
  | annotation_type_declaration {: return new MemberInterfaceDecl(annotation_type_declaration); :}
  | SEMICOLON {: return new StaticInitializer(new Block()); :}
  ;

Opt default_value_opt =
                  {: return new Opt(); :}
  | default_value {: return new Opt(default_value); :}
  ;

ElementValue default_value =
  DEFAULT element_value {: return element_value; :}
  ;


Modifier modifier =
  annotation
  ;

Annotation annotation =
    normal_annotation
  | marker_annotation
  | single_element_annotation
  ;

Modifier normal_annotation =
  AT type LPAREN element_value_pairs_opt RPAREN {: return new Annotation("annotation", type, element_value_pairs_opt); :}
  ;

List element_value_pairs_opt =
    {: return new List(); :}
  | element_value_pairs {: return element_value_pairs; :}
  ;

List element_value_pairs =
    element_value_pair {: return new List().add(element_value_pair); :}
  | element_value_pairs COMMA element_value_pair {: return element_value_pairs.add(element_value_pair); :}
  ;

ElementValuePair element_value_pair =
  IDENTIFIER.id EQ element_value {: return new ElementValuePair(id, element_value); :}
  ;

ElementValue element_value =
  conditional_expression              {: return new ElementConstantValue(conditional_expression); :}
  | annotation                        {: return new ElementAnnotationValue(annotation); :}
  | element_value_array_initializer.e {: return e; :}
  ;

ElementArrayValue element_value_array_initializer =
  LBRACE element_values_opt RBRACE {: return new ElementArrayValue(element_values_opt); :}
  | LBRACE element_values_opt COMMA RBRACE {: return new ElementArrayValue(element_values_opt); :}
  ;

List element_values_opt =
    {: return new List(); :}
  | element_values {: return element_values; :}
  ;

List element_values =
    element_value {: return new List().add(element_value); :}
  | element_values COMMA element_value {: return element_values.add(element_value); :}
  ;
  
Modifier marker_annotation =
  AT type {: return new Annotation("annotation", type, new List()); :}
  ;
  
Modifier single_element_annotation =
  AT type LPAREN element_value RPAREN {: return new Annotation("annotation", type, new List().add(new ElementValuePair("value", element_value))); :}
  ;
  

CompilationUnit compilation_unit =
    modifiers.a package_declaration.p import_declarations.i type_declarations.t 
      {: return new AnnotatedCompilationUnit(p.getID(), i, t, a); :}
  | modifiers.a package_declaration.p                       type_declarations.t
      {: return new AnnotatedCompilationUnit(p.getID(), new List(), t, a); :}
  | modifiers.a package_declaration.p import_declarations.i                     
      {: return new AnnotatedCompilationUnit(p.getID(), i, new List(), a); :}
  | modifiers.a package_declaration.p                                           
      {: return new AnnotatedCompilationUnit(p.getID(), new List(), new List(), a); :}
  ;
Stmt statement = 
  enhanced_for_statement.f                             {: return f; :}
  ; 

Stmt statement_no_short_if = 
	enhanced_for_statement_no_short_if.f                 {: return f; :}
	;

Stmt enhanced_for_statement =
	FOR LPAREN formal_parameter.p COLON expression.e RPAREN statement.s
	{:  return new EnhancedForStmt(p, e, s);  :}
	;

Stmt enhanced_for_statement_no_short_if =
	FOR LPAREN formal_parameter.p COLON expression.e RPAREN statement_no_short_if.s 
	{: return new EnhancedForStmt(p, e, s);  :}
	;    
TypeDecl type_declaration =
  enum_declaration.i {:  return i;  :}
  ;

BodyDecl interface_member_declaration =
  enum_declaration.i {:  return new MemberClassDecl(i);  :}
  ;

BodyDecl class_member_declaration =
  enum_declaration.i {:  return new MemberClassDecl(i);  :}
  ;

EnumDecl enum_declaration =
    modifiers ENUM IDENTIFIER.id interfaces enum_body {: return new EnumDecl(modifiers, id, interfaces, enum_body); :}
  |           ENUM IDENTIFIER.id interfaces enum_body {: return new EnumDecl(new Modifiers(new List()), id, interfaces, enum_body); :}
  | modifiers ENUM IDENTIFIER.id            enum_body {: return new EnumDecl(modifiers, id, new List(), enum_body); :}
  |           ENUM IDENTIFIER.id            enum_body {: return new EnumDecl(new Modifiers(new List()), id, new List(), enum_body); :}
  ;

List enum_body =
    LBRACE RBRACE {: return new List(); :}
  | LBRACE enum_constants RBRACE {: return enum_constants; :}
  | LBRACE COMMA RBRACE {: return new List(); :}
  | LBRACE enum_constants COMMA RBRACE {: return enum_constants; :}
  | LBRACE enum_body_declarations RBRACE {: return enum_body_declarations; :}
  | LBRACE enum_constants.c enum_body_declarations.d RBRACE
    {: for(int i = 0; i < d.getNumChild(); i++) c.add(d.getChildNoTransform(i)); return c; :}
  | LBRACE COMMA enum_body_declarations RBRACE {: return enum_body_declarations; :}
  | LBRACE enum_constants.c COMMA enum_body_declarations.d RBRACE
    {: for(int i = 0; i < d.getNumChild(); i++) c.add(d.getChildNoTransform(i)); return c; :}
  ;

List enum_constants =
    enum_constant {: return new List().add(enum_constant); :}
  | enum_constants COMMA enum_constant {: return enum_constants.add(enum_constant); :}
  ;

BodyDecl enum_constant =
                IDENTIFIER.id
    {: return new EnumConstant(new Modifiers(new List()), id, new List(), new List()); :}
  | annotations IDENTIFIER.id
    {: return new EnumConstant(new Modifiers(annotations), id, new List(), new List()); :}
  |             IDENTIFIER.id arguments
    {: return new EnumConstant(new Modifiers(new List()), id, arguments, new List()); :}
  | annotations IDENTIFIER.id arguments
    {: return new EnumConstant(new Modifiers(annotations), id, arguments, new List()); :}
  |             IDENTIFIER.id           class_body
    {: return new EnumConstant(new Modifiers(new List()), id, new List(), class_body); :}
  | annotations IDENTIFIER.id           class_body 
    {: return new EnumConstant(new Modifiers(annotations), id, new List(), class_body); :}
  |             IDENTIFIER.id arguments class_body
    {: return new EnumConstant(new Modifiers(new List()), id, arguments, class_body); :}
  | annotations IDENTIFIER.id arguments class_body 
    {: return new EnumConstant(new Modifiers(annotations), id, arguments, class_body); :}
  ;

List arguments =
  LPAREN argument_list RPAREN {: return argument_list; :}
  ;

List annotations =
    annotation             {: return new List().add(annotation); :}
  | annotations annotation {: return annotations.add(annotation); :}
  ;

List enum_body_declarations =
  SEMICOLON class_body_declarations_opt {: return class_body_declarations_opt; :}
  ;

List class_body_declarations_opt =
  {: return new List(); :}
  | class_body_declarations.d {: return d; :}
  ;
ExprStmt explicit_constructor_invocation =
		type_arguments.a THIS LPAREN argument_list_opt.l RPAREN SEMICOLON 
		{:  return new ExprStmt(new ParConstructorAccess("this", l, a));  :}
	|	type_arguments.a SUPER LPAREN argument_list_opt.l RPAREN SEMICOLON 
		{:  return new ExprStmt(new ParSuperConstructorAccess("super", l, a));  :}
	|	primary.p DOT type_arguments.a SUPER LPAREN argument_list_opt.l RPAREN SEMICOLON 
		{:  return new ExprStmt(p.qualifiesAccess(new ParSuperConstructorAccess("super", l, a))); :}
	|	name.n DOT type_arguments.a SUPER LPAREN argument_list_opt.l RPAREN SEMICOLON 
		{:  return new ExprStmt(n.qualifiesAccess(new ParSuperConstructorAccess("super", l, a))); :}
	;
	
Access method_invocation =
		primary.p DOT type_arguments.a IDENTIFIER.id LPAREN argument_list_opt.l RPAREN 
		{: return p.qualifiesAccess(new ParMethodAccess(id, l, a));  :}
	|	name.n DOT type_arguments.a IDENTIFIER.id LPAREN argument_list_opt.l RPAREN 
		{: return n.qualifiesAccess(new ParMethodAccess(id, l, a));  :}
	|	SUPER DOT type_arguments.a IDENTIFIER.id LPAREN argument_list_opt.l RPAREN 
		{: return new SuperAccess("super").qualifiesAccess(
		                    new ParMethodAccess(id, l, a));  :}
	|	name.n DOT.d1 SUPER DOT.d2 type_arguments.a IDENTIFIER.id LPAREN argument_list_opt.l RPAREN 
		{: return n.qualifiesAccess(new SuperAccess("super")).qualifiesAccess(
                            new ParMethodAccess(id, l, a));  :}
	;

List throws_opt =
   {: return new List(); :}
 | throws.t {: return t; :}
 ;
  
MethodDecl method_header =
		modifiers.m LT type_parameter_list_1.l type.t par_method_declarator.d throws_opt.tl 
		{:  d.setModifiers(m); d.setTypeAccess(t); d.setExceptionList(tl); 
		   d.setTypeParameterList(l); return d;  :}
	|	            LT type_parameter_list_1.l type.t par_method_declarator.d throws_opt.tl 
		{:  d.setModifiers(new Modifiers(new List())); d.setTypeAccess(t); d.setExceptionList(tl); 
		   d.setTypeParameterList(l); return d;  :}
	|	modifiers.m LT type_parameter_list_1.l VOID par_method_declarator.d throws_opt.tl 
		{:  d.setModifiers(m); d.setTypeAccess(new PrimitiveTypeAccess("void"));
		   d.setExceptionList(tl); d.setTypeParameterList(l); return d;  :}
	|	            LT type_parameter_list_1.l VOID par_method_declarator.d throws_opt.tl 
		{:  d.setModifiers(new Modifiers(new List())); d.setTypeAccess(new PrimitiveTypeAccess("void"));
		   d.setExceptionList(tl); d.setTypeParameterList(l); return d;  :}
	;

ConstructorDecl constructor_declaration =
		modifiers.m LT type_parameter_list_1.l IDENTIFIER.id LPAREN formal_parameter_list_opt.pl RPAREN throws_opt.tl generic_constructor_body.b 
		{:  b.setModifiers(m); b.setID(id); b.setParameterList(pl); b.setExceptionList(tl);  
		   b.setTypeParameterList(l); return b; :}
	|	            LT type_parameter_list_1.l IDENTIFIER.id LPAREN formal_parameter_list_opt.pl RPAREN throws_opt.tl generic_constructor_body.b 
		{:  b.setModifiers(new Modifiers(new List())); b.setID(id); b.setParameterList(pl); b.setExceptionList(tl);  
		   b.setTypeParameterList(l); return b; :}
	;

GenericConstructorDecl generic_constructor_body =
		LBRACE explicit_constructor_invocation.c
			block_statements.l RBRACE                           {: return new GenericConstructorDecl(null, null, null, null, new Opt(c), new Block(l), null, new List()); :}
	|	LBRACE explicit_constructor_invocation.c RBRACE       {: return new GenericConstructorDecl(null, null, null, null, new Opt(c), new Block(new List()), null, new List()); :}
	|	LBRACE block_statements.l RBRACE                      {: return new GenericConstructorDecl(null, null, null, null, new Opt(), new Block(l), null, new List()); :}
	|	LBRACE RBRACE                                         {: return new GenericConstructorDecl(null, null, null, null, new Opt(), new Block(new List()), null, new List()); :}
	;


List formal_parameter_list_opt =
  {: return new List(); :}
  | formal_parameter_list.l {: return l; :}
  ;
   
GenericMethodDecl par_method_declarator =
		IDENTIFIER.id LPAREN formal_parameter_list_opt.p RPAREN 
		{: return new GenericMethodDecl(null, null, id, p, new List(), null, new Opt(), null, new List()); :}
	|	par_method_declarator.m LBRACK RBRACK {: m.addEmptyBracket(new EmptyBracket()); return m; :}
	;

ClassDecl class_declaration =
		modifiers.m CLASS IDENTIFIER.id type_parameters.p super_opt.s interfaces_opt.i class_body.b 
		{:  return new GenericClassDecl(m, id, s, i, b, p, new List());  :}
	|	            CLASS IDENTIFIER.id type_parameters.p super_opt.s interfaces_opt.i class_body.b 
		{:  return new GenericClassDecl(new Modifiers(new List()), id, s, i, b, p, new List());  :}
	;

Opt super_opt =
  {: return new Opt(); :}
  | super.s {: return s; :}
  ;

List interfaces_opt =
    {: return new List(); :}
  | interfaces.i {: return i; :}
  ;

List extends_interfaces_opt =
                       {: return new List(); :}
  | extends_interfaces {: return extends_interfaces; :}
  ;
  
InterfaceDecl interface_declaration =
		modifiers.m INTERFACE IDENTIFIER.id type_parameters.p extends_interfaces_opt.i interface_body.b 
		{:  return new GenericInterfaceDecl(m, id, i, b, p, new List());  :}
	|             INTERFACE IDENTIFIER.id type_parameters.p extends_interfaces_opt.i interface_body.b 
		{:  return new GenericInterfaceDecl(new Modifiers(new List()), id, i, b, p, new List());  :}
	;

Access class_or_interface_type =
		name.t LT type_argument_list_1.l {:  return new ParTypeAccess(t, l);  :}
  | class_or_interface_type.t DOT IDENTIFIER.id {: return t.qualifiesAccess(new ParseName(id)); :}
  | class_or_interface_type.t DOT IDENTIFIER.id LT type_argument_list_1.l 
    {: return new ParTypeAccess(t.qualifiesAccess(new ParseName(id)), l); :}
	;
Access array_type =
    	name.t LT type_argument_list_1.l DOT name.n dims.d 
    	{:  return new ArrayTypeAccess(new ParTypeAccess(t, l).qualifiesAccess(n), d.getNumChild());  :}
	|	name.t LT type_argument_list_1.l dims.d 
		{:  return new ArrayTypeAccess(new ParTypeAccess(t, l), d.getNumChild());  :}
	;

List type_arguments =
		LT type_argument_list_1.l {:  return l;  :}
	;
      
Access wildcard =
		QUESTION                             {: return new Wildcard(); :}
	|	QUESTION EXTENDS reference_type.t    {: return new WildcardExtends(t); :}
    |	QUESTION SUPER reference_type.t    {: return new WildcardSuper(t); :}
    ;
   
Access wildcard_1 =
		QUESTION GT                          {: return new Wildcard(); :}
	|	QUESTION EXTENDS reference_type_1.t  {: return new WildcardExtends(t); :}
	|	QUESTION SUPER reference_type_1.t    {: return new WildcardSuper(t); :}
	;
	
Access wildcard_2 =
		QUESTION RSHIFT                      {: return new Wildcard(); :}
	|	QUESTION EXTENDS reference_type_2.t  {: return new WildcardExtends(t); :}
	|	QUESTION SUPER reference_type_2.t    {: return new WildcardSuper(t); :}
	;
	
Access wildcard_3 =
		QUESTION URSHIFT                     {: return new Wildcard(); :}
	|	QUESTION EXTENDS reference_type_3.t  {: return new WildcardExtends(t); :}
	|	QUESTION SUPER reference_type_3.t    {: return new WildcardSuper(t); :}
	;

Opt subclass_body_opt =
    {: return new Opt(); :}
  | subclass_body {: return subclass_body; :}
  ;

Expr class_instance_creation_expression =
	 	NEW type_arguments.a class_or_interface_type.t LPAREN argument_list_opt.l RPAREN subclass_body_opt.b
		{:  return new ParClassInstanceExpr(t, l, b, a); :}
    /*
	|	primary.n DOT NEW IDENTIFIER.id type_arguments.a LPAREN argument_list_opt.l RPAREN subclass_body_opt.b
        {:  return new NestedClassInstanceExpr(n, new ParTypeAccess(new ParseName(id), a), l, b ); :}	
        */
	|	primary.n DOT NEW type_arguments.ca IDENTIFIER.id LPAREN argument_list_opt.l RPAREN class_body_opt.b
		{:  return n.qualifiesAccess(new ParClassInstanceExpr(new ParseName(id), l, b, ca)); :}
    /*
	|	primary.n DOT NEW type_arguments.ca IDENTIFIER.id type_arguments.ta LPAREN argument_list_opt.l RPAREN class_body_opt.b
		{:  return new ParNestedClassInstanceExpr(n, new ParTypeAccess(new ParseName(id), ta), l, b, ca); :}
	|	name.n DOT NEW IDENTIFIER.id type_arguments.a LPAREN argument_list_opt.l RPAREN subclass_body_opt.b
		{:  return new NestedClassInstanceExpr(n, new ParTypeAccess(new ParseName(id), a), l, b);  :}
	|	name.n DOT NEW type_arguments.ca IDENTIFIER.id LPAREN argument_list_opt.l RPAREN class_body_opt.b
		{:  return new ParNestedClassInstanceExpr(n, new ParseName(id), l, b, ca);  :}
	|	name.n DOT NEW type_arguments.ca IDENTIFIER.id type_arguments.ta LPAREN argument_list_opt.l RPAREN class_body_opt.b
		{:  return new ParNestedClassInstanceExpr(n, new ParTypeAccess(new ParseName(id), ta), l, b, ca);  :}
    */
	;

List type_argument_list =
		type_argument.a {:  return new List().add(a);  :}
	|	type_argument_list.l COMMA type_argument.a {:  return l.add(a);  :}
	;
	
List type_argument_list_1 =
		type_argument_1.a {:  return new List().add(a);  :}
	|	type_argument_list.l COMMA type_argument_1.a {:  return l.add(a);  :}
	;
	
List type_argument_list_2 =
		type_argument_2.a {:  return new List().add(a);  :}
	|	type_argument_list.l COMMA type_argument_2.a {:  return l.add(a);  :}
	;
	
List type_argument_list_3 =
		type_argument_3.a {:  return new List().add(a);  :}
	|	type_argument_list.l COMMA type_argument_3.a {:  return l.add(a);  :}
	;
	
Access type_argument =
		reference_type.t {:  return t;  :}
	|	wildcard.w {: return w; :}
	;
	
Access type_argument_1 =
		reference_type_1.t {:  return t;  :}
	|	wildcard_1.w {: return w; :}
	;
	
Access type_argument_2 =
		reference_type_2.t {:  return t;  :}
	|	wildcard_2.w {: return w; :}
	;
	
Access type_argument_3 =
		reference_type_3.t {:  return t;  :}
	|	wildcard_3.w {: return w; :}
	;
  
Access reference_type_1 =
		reference_type.t GT {:  return t;  :}
	|	name.t LT type_argument_list_2.l {:  return new ParTypeAccess(t, l);  :}
	;
	
Access reference_type_2 =
		reference_type.t RSHIFT {:  return t;  :}
	|	name.t LT type_argument_list_3.l {:  return new ParTypeAccess(t, l);  :}
	;
	
Access reference_type_3 =
		reference_type.t URSHIFT {:  return t;  :}
	;
Expr cast_expression =
		LPAREN name.n LT type_argument_list_1.a RPAREN unary_expression_not_plus_minus.e 
		{:  return new CastExpr(new ParTypeAccess(n, a), e);  :}
	|	LPAREN name.n LT type_argument_list_1.a dims.d RPAREN unary_expression_not_plus_minus.e 
		{:  return new CastExpr(new ArrayTypeAccess(new ParTypeAccess(n, a), d.getNumChild()), e);  :}
	|	LPAREN name.n LT type_argument_list_1.a DOT class_or_interface_type.t RPAREN unary_expression_not_plus_minus.e 
		{:  return new CastExpr(new ParTypeAccess(n, a).qualifiesAccess(t), e);  :}
	|	LPAREN name.n LT type_argument_list_1.a DOT class_or_interface_type.t dims.d RPAREN unary_expression_not_plus_minus.e 
		{:  return new CastExpr(new ArrayTypeAccess(new ParTypeAccess(n, a).qualifiesAccess(t), d.getNumChild()), e);  :}
  ;
List type_parameters =
		LT type_parameter_list_1.l {:  return l;  :}
	;
	
List type_parameter_list =
		type_parameter_list.l COMMA type_parameter.p {:  return l.add(p);  :}
	|	type_parameter.p {:  return new List().add(p);  :}
	;
	
List type_parameter_list_1 =
		type_parameter_1.p {:  return new List().add(p);  :}
	|	type_parameter_list.l COMMA type_parameter_1.p {:  return l.add(p);  :}
	;
	
TypeVariable type_parameter =
		IDENTIFIER.v              {:  return new TypeVariable(new Modifiers(), v, new List(), new List());  :}
	|	IDENTIFIER.v type_bound.l {:  return new TypeVariable(new Modifiers(), v, new List(), l); :}
	;
	
TypeVariable type_parameter_1 =
		IDENTIFIER.v GT             {:  return new TypeVariable(new Modifiers(), v, new List(), new List());  :}
  |	IDENTIFIER.v type_bound_1.l {:  return new TypeVariable(new Modifiers(), v, new List(), l);  :}
	;
	
List type_bound =
		EXTENDS reference_type.t {:  return new List().add(t);  :}
	|	EXTENDS reference_type.t additional_bound_list.l {:  l.insertChild(t,0); return l; :}
    ;
    
List type_bound_1 =
		EXTENDS reference_type_1.t {:  return new List().add(t);  :}
	|	EXTENDS reference_type.t additional_bound_list_1.l {:  l.insertChild(t,0); return l; :}
    ;
    
List additional_bound_list =
		additional_bound.b additional_bound_list.l {:  l.insertChild(b,0); return l; :}
	|	additional_bound.b {:  return new List().add(b);  :}
    ;
    
List additional_bound_list_1 =
		additional_bound.b additional_bound_list_1.l {:  l.insertChild(b,0); return l; :}
	|	additional_bound_1.b {:  return new List().add(b);  :}
    ;
    
Access additional_bound =
		AND interface_type.t {:  return t;  :}
	;
	
Access additional_bound_1 =
		AND reference_type_1.t {:  return t;  :}
	;

Opt class_body_opt = {: return new Opt(); :}
    |  class_body.b {:  return new Opt(b);  :};

ImportDecl import_declaration =
      single_static_import_declaration.s {:  return s;  :}
    |  static_import_on_demand_declaration.t {:  return t;  :}
    ;

ImportDecl single_static_import_declaration =
      IMPORT STATIC name.n DOT IDENTIFIER.id SEMICOLON {: return new SingleStaticImportDecl(n, id);  :};

ImportDecl static_import_on_demand_declaration =
      IMPORT STATIC name.n DOT MULT SEMICOLON {: return new StaticImportOnDemandDecl(n);  :};
ParameterDeclaration formal_parameter =
    type.t ELLIPSIS variable_declarator_id.v
    {:  return new VariableArityParameterDeclaration(new Modifiers(new List()), t, v.getID(), v.getEmptyBracketList());  :}
  | modifiers.m type.t ELLIPSIS variable_declarator_id.v 
    {:  return new VariableArityParameterDeclaration(
                                                                   m,
                                                                   t,
                                                                   v.getID(),
                                                                   v.getEmptyBracketList()
                                                                 );
                                                               :}
     ;

Expr primary_no_new_array :=
    literal.l                                  {: return l; :}
  | primitive_type.n DOT class_literal.c       {: return n.qualifiesAccess(c); :}
  | primitive_type.n dims.d DOT class_literal.c {: ArrayTypeAccess a = new ArrayTypeAccess(n, d.getNumChild());
                                                   a.setStart(n.getStart());
                                                   a.setEnd(d.getEnd());
                                                   return a.qualifiesAccess(c); :}
  | name.n DOT class_literal.c                 {: return n.qualifiesAccess(c); :}
  | name.n dims.d DOT class_literal.c          {: ArrayTypeAccess a = new ArrayTypeAccess(n, d.getNumChild());
                                                  a.setStart(n.getStart());
                                                  a.setEnd(d.getEnd());
                                                  return a.qualifiesAccess(c); :}
  | VOID DOT class_literal.c                   {: PrimitiveTypeAccess v = new PrimitiveTypeAccess("void");
                                                  v.setStart(VOID.getStart());
                                                  v.setEnd(VOID.getEnd());
                                                  return v.qualifiesAccess(c); :}
  | THIS                                       {: return new ThisAccess("this"); :}
  | name.n DOT THIS                            {: ThisAccess t = new ThisAccess("this");
                                                  t.setStart(THIS.getStart()); t.setEnd(THIS.getEnd());
                                                  return n.qualifiesAccess(t); :}
  | LPAREN expression_nn.e RPAREN                 {: return new ParExpr(e); :}
  | LPAREN name.n RPAREN                       {: return new ParExpr(n); :}
  | class_instance_creation_expression.c       {: return c; :}
  | field_access.f                             {: return f; :}
  | method_invocation.m                        {: return m; :}
  | array_access.a                             {: return a; :}
  ;


Expr postfix_expression_nn =
    primary.p                                  {: return p; :}
  | postincrement_expression.e                 {: return e; :}
  | postdecrement_expression.e                 {: return e; :}
  ;
Expr unary_expression_nn =
    preincrement_expression.e                  {: return e; :}
  | predecrement_expression.e                  {: return e; :}
  | PLUS unary_expression.e                    {: return new PlusExpr(e); :}
  | MINUS unary_expression.e                   {: return new MinusExpr(e); :}
  | unary_expression_not_plus_minus_nn.e          {: return e; :}
  ;
Expr unary_expression_not_plus_minus_nn =
    postfix_expression_nn.e                       {: return e; :}
  | COMP unary_expression.e                    {: return new BitNotExpr(e); :}
  | NOT unary_expression.e                     {: return new LogNotExpr(e); :}
  | cast_expression.e                          {: return e; :}
  ;
Expr multiplicative_expression_nn =
    unary_expression_nn.e                                     {: return e; :}
  | name.e1 MULT unary_expression.e2  {: return new MulExpr(e1, e2); :}
  | multiplicative_expression_nn.e1 MULT unary_expression.e2  {: return new MulExpr(e1, e2); :}
  | name.e1 DIV unary_expression.e2   {: return new DivExpr(e1, e2); :}
  | multiplicative_expression_nn.e1 DIV unary_expression.e2   {: return new DivExpr(e1, e2); :}
  | name.e1 MOD unary_expression.e2   {: return new ModExpr(e1, e2); :}
  | multiplicative_expression_nn.e1 MOD unary_expression.e2   {: return new ModExpr(e1, e2); :}
  ;
Expr additive_expression_nn =
    multiplicative_expression_nn.e                                {: return e; :}
  | name.e1 PLUS multiplicative_expression.e2   {: return new AddExpr(e1, e2); :}
  | additive_expression_nn.e1 PLUS multiplicative_expression.e2   {: return new AddExpr(e1, e2); :}
  | name.e1 MINUS multiplicative_expression.e2  {: return new SubExpr(e1, e2); :}
  | additive_expression_nn.e1 MINUS multiplicative_expression.e2  {: return new SubExpr(e1, e2); :}
  ;
Expr shift_expression_nn =
    additive_expression_nn.e                               {: return e; :}
  | name.e1 LSHIFT additive_expression.e2   {: return new LShiftExpr(e1, e2); :}
  | shift_expression_nn.e1 LSHIFT additive_expression.e2   {: return new LShiftExpr(e1, e2); :}
  | name.e1 RSHIFT additive_expression.e2   {: return new RShiftExpr(e1, e2); :}
  | shift_expression_nn.e1 RSHIFT additive_expression.e2   {: return new RShiftExpr(e1, e2); :}
  | name.e1 URSHIFT additive_expression.e2  {: return new URShiftExpr(e1, e2); :}
  | shift_expression_nn.e1 URSHIFT additive_expression.e2  {: return new URShiftExpr(e1, e2); :}
  ;
Expr relational_expression_nn =
    shift_expression_nn.e                                  {: return e; :}
  | name.e1 LT shift_expression.e2     {: return new LTExpr(e1, e2); :}
  | shift_expression_nn.e1 LT shift_expression.e2     {: return new LTExpr(e1, e2); :}
  | name.e1 GT shift_expression.e2     {: return new GTExpr(e1, e2); :}
  | shift_expression_nn.e1 GT shift_expression.e2     {: return new GTExpr(e1, e2); :}
  | name.e1 LTEQ shift_expression.e2   {: return new LEExpr(e1, e2); :}
  | relational_expression_nn.e1 LTEQ shift_expression.e2   {: return new LEExpr(e1, e2); :}
  | name.e1 GTEQ shift_expression.e2   {: return new GEExpr(e1, e2); :}
  | relational_expression_nn.e1 GTEQ shift_expression.e2   {: return new GEExpr(e1, e2); :}
  | name.e INSTANCEOF reference_type.t {: return new InstanceOfExpr(e, t); :}
  | relational_expression_nn.e INSTANCEOF reference_type.t {: return new InstanceOfExpr(e, t); :}
  ;
Expr equality_expression_nn =
    relational_expression_nn.e                               {: return e; :}
  | name.e1 EQEQ relational_expression.e2  {: return new EQExpr(e1, e2); :}
  | equality_expression_nn.e1 EQEQ relational_expression.e2  {: return new EQExpr(e1, e2); :}
  | name.e1 NOTEQ relational_expression.e2 {: return new NEExpr(e1, e2); :}
  | equality_expression_nn.e1 NOTEQ relational_expression.e2 {: return new NEExpr(e1, e2); :}
  ;
Expr and_expression_nn =
    equality_expression_nn.e                               {: return e; :}
  | name.e1 AND equality_expression.e2        {: return new AndBitwiseExpr(e1, e2); :}
  | and_expression_nn.e1 AND equality_expression.e2        {: return new AndBitwiseExpr(e1, e2); :}
  ;
Expr exclusive_or_expression_nn =
    and_expression_nn.e                                    {: return e; :}
  | name.e1 XOR and_expression.e2    {: return new XorBitwiseExpr(e1, e2); :}
  | exclusive_or_expression_nn.e1 XOR and_expression.e2    {: return new XorBitwiseExpr(e1, e2); :}
  ;
Expr inclusive_or_expression_nn =
    exclusive_or_expression_nn.e                                {: return e; :}
  | name.e1 OR exclusive_or_expression.e2 {: return new OrBitwiseExpr(e1, e2); :}
  | inclusive_or_expression_nn.e1 OR exclusive_or_expression.e2 {: return new OrBitwiseExpr(e1, e2); :}
  ;
Expr conditional_and_expression_nn =
    inclusive_or_expression_nn.e                                       {: return e; :}
  | name.e1 ANDAND inclusive_or_expression.e2 {: return new AndLogicalExpr(e1, e2); :}
  | conditional_and_expression_nn.e1 ANDAND inclusive_or_expression.e2 {: return new AndLogicalExpr(e1, e2); :}
  ;
Expr conditional_or_expression_nn =
    conditional_and_expression_nn.e                                    {: return e; :}
  | name.e1 OROR conditional_and_expression.e2 {: return new OrLogicalExpr(e1, e2); :}
  | conditional_or_expression_nn.e1 OROR conditional_and_expression.e2 {: return new OrLogicalExpr(e1, e2); :}
  ;
Expr conditional_expression_nn =
    conditional_or_expression_nn.c                                                        {: return c; :}
  | name.c QUESTION expression.e1 COLON conditional_expression.e2 {: return new ConditionalExpr(c, e1, e2); :}
  | conditional_or_expression_nn.c QUESTION expression.e1 COLON conditional_expression.e2 {: return new ConditionalExpr(c, e1, e2); :}
  ;
Expr assignment_expression_nn =
    conditional_expression_nn.c {: return c; :}
  | assignment.a {: return a; :}
  ;

Expr expression_nn =
    assignment_expression_nn
  ;

